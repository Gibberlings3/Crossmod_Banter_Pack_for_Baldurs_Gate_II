// inline file needed for component checks
<<<<<<<<./%MOD_FOLDER%/inline/tp2_check.tpa
ACTION_IF MOD_IS_INSTALLED ~%primary_params_3%~ ~%component%~ BEGIN
  OUTER_SET check = 1
END
>>>>>>>>

OUTER_SPRINT conflict_found   @5
OUTER_SPRINT conflict_missing @6

<<<<<<<<./inline/log.txt
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Crossmod Installation log                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// System Info                                      \\\\\
/////                                                  \\\\\

 == WeiDU thinks that this is Component %COMPONENT_NUMBER%
 == Author: %TP2_AUTHOR%
 == Language = %LANGUAGE%
 == Architecture = %WEIDU_ARCH%
 == Operating System = %WEIDU_OS%
 == User Directory = %USER_DIRECTORY%
 == Running .exe = %WEIDU_EXECUTABLE%
 == Mod name (as seen by WeiDU) = %TP2_FILE_NAME%
 == Interactive Installation? %INTERACTIVE% (1 is interactive, 0 is being invoked by another process)
 
/////                                                  \\\\\
///// Romance conflict checks                          \\\\\
/////                                                  \\\\\
>>>>>>>>

OUTER_SET index = 1
OUTER_WHILE (index > 0) BEGIN

  ACTION_IF FILE_EXISTS ~weidu_external/%MOD_FOLDER%/logs/conflicts_log_%index%.txt~ BEGIN
    OUTER_SET index = index + 1
  END ELSE BEGIN  
    OUTER_SPRINT logfile ~weidu_external/%MOD_FOLDER%/logs/conflicts_log_%index%.txt~ 
    COPY + ~./inline/log.txt~ ~%logfile%~ EVALUATE_BUFFER
    OUTER_SET index = 0
  END

END
  
INCLUDE ~%MOD_FOLDER%/lib/mod_array.tpa~

// build 'working' array from mods actually present
ACTION_PHP_EACH mod_array AS primary_params => primary_check BEGIN

  ACTION_FOR_EACH game IN SoA ToB BEGIN

    OUTER_SPRINT file      ~%primary_params_4%~
    OUTER_SPRINT component ~%primary_params_5%~
    OUTER_SPRINT script    ~%primary_params_8%~
    ACTION_IF ("%game%" STRING_COMPARE_CASE "tob" = 0)  BEGIN // 4/5/8 are columns for base game (bg, bg2, or iwd), use 6/7/9 for expansions (sod, tob, how)    
      ACTION_IF game_is_soa BEGIN // without tob, blank entries so all of these get skipped
        OUTER_SPRINT file      ~~
        OUTER_SPRINT component ~~
        OUTER_SPRINT script    ~~
      END ELSE BEGIN // otherwise use legit values
        OUTER_SPRINT file      ~%primary_params_6%~
        OUTER_SPRINT component ~%primary_params_7%~
        OUTER_SPRINT script    ~%primary_params_9%~    
      END  
    END 
    
    ACTION_IF (("%script%" STRING_COMPARE_CASE "") AND (FILE_EXISTS_IN_GAME ~%script%.bcs~))  BEGIN // only proceed if conflict script entryis non-blank and exists
    
      OUTER_SET check = 0 
      ACTION_IF (IS_AN_INT "%component%") BEGIN // component specified, check tp2
      
        // have to do weird external tpa routine since MOD_IS_INSTALLED won't take variables directly
        COPY ~./%MOD_FOLDER%/inline/tp2_check.tpa~ ~weidu_external/%MOD_FOLDER%/tp2_check.tpa~ EVALUATE_BUFFER
        REINCLUDE ~weidu_external/%MOD_FOLDER%/tp2_check.tpa~
        
      END ELSE BEGIN
      
        ACTION_IF (("%file%" STRING_COMPARE_CASE "") AND (FILE_EXISTS_IN_GAME ~%file%~)) BEGIN // must simpler check if file specified
        
          OUTER_SET check = 1  
          
        END
        
      END  
      
      ACTION_IF !check BEGIN // mod not detected as installed, log it and move on
        
        APPEND_OUTER + ~%logfile%~ ~%WNL%   -- %primary_params_0% (%primary_params_1%)%conflict_missing%%game%~ KEEP_CRLF 
        
      END ELSE BEGIN // mod detected as installed
      
        // for mods which use the same script for tob and soa, set soa extension as tob extension
        ACTION_IF (("%game%" STRING_COMPARE_CASE "tob" = 0) AND (!FILE_EXISTS ~%MOD_FOLDER%/conflicts/%primary_params_2%/%script%.baf~)) BEGIN
          OUTER_SPRINT extend ~%primary_params_8%~
        END ELSE BEGIN  
          OUTER_SPRINT extend ~%script%~
        END  
        
        APPEND_OUTER + ~%logfile%~ ~%WNL% == %primary_params_0% (%primary_params_1%)%conflict_found%%game%~ KEEP_CRLF 
        
        EXTEND_TOP ~%script%.bcs~ ~%MOD_FOLDER%/conflicts/%primary_params_2%/%extend%.baf~
        
      END // closing !check 
      
    END // closing romance conflict file check   
    
  END // closing SoA/ToB branching

END // closing PHP_EACH of primary NPC
